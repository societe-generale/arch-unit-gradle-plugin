
plugins {
    id 'java'
    id 'com.gradle.plugin-publish' version '0.10.1'
    id 'java-gradle-plugin'
    id 'net.researchgate.release' version '2.6.0'
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    implementation 'com.societegenerale.commons:arch-unit-build-plugin-core:2.5.1'
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

signing {
    sign configurations.archives
}

def v_ossrhUsername="FOO"
def v_ossrhPassword="BAR"

if (project.hasProperty("ossrhUsername")) {
    v_ossrhUsername = ossrhUsername
}
if (project.hasProperty("ossrhPassword")) {
    v_ossrhPassword = ossrhPassword
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: v_ossrhUsername, password: v_ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: v_ossrhUsername, password: v_ossrhPassword)
            }

            pom.project {
                name 'ArchUnit Gradle Plugin'
                packaging 'jar'
                artifactId archivesBaseName
                version version
                description 'A Gradle plugin to run packaged ArchUnit rules'
                url 'https://github.com/societe-generale/arch-unit-gradle-plugin'

                scm {
                    connection 'scm:git:git://github.com/societe-generale/arch-unit-gradle-plugin.git'
                    developerConnection 'scm:git:git@github.com:societe-generale/arch-unit-gradle-plugin.git'
                    url 'https://github.com/societe-generale/arch-unit-gradle-plugin'
                }

                licenses {
                    license {
                        name 'Apache 2'
                        url 'https://github.com/societe-generale/arch-unit-gradle-plugin/blob/master/LICENSE.md'
                    }
                }

                developers {
                    developer {
                        id 'vincent-fuchs'
                        name 'Vincent FUCHS'
                    }
                    developer {
                        id 'MarvinKern'
                        name 'Marvin KERN'
                    }
                }
            }
        }
    }
}

gradlePlugin {
    plugins {
        archUnitGradlePlugin {
            id = 'com.societegenerale.common.arch-unit-gradle-plugin'
            implementationClass = 'com.societegenerale.commons.plugin.gradle.ArchUnitGradlePlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/societe-generale/arch-unit-gradle-plugin'
    vcsUrl = 'https://github.com/societe-generale/arch-unit-gradle-plugin'
    description = 'A Gradle plugin to run packaged ArchUnit rules'
    tags = ['archUnit']

    plugins {
        archUnitGradlePlugin {
            // id is captured from java-gradle-plugin configuration
            displayName = 'A Gradle plugin to run packaged ArchUnit rules'
        }
    }
}

afterReleaseBuild.dependsOn uploadArchives
afterReleaseBuild.dependsOn publishPlugins
