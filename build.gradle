
plugins {
    id 'com.gradle.plugin-publish' version '0.12.0'
    id 'java-gradle-plugin'
    id 'groovy'

    //useful to publish SNAPSHOT versions locally during tests
    id 'maven-publish'
}

sourceCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation 'com.societegenerale.commons:arch-unit-build-plugin-core:2.7.3'
}

testing {
    suites {
        test {
            useJUnitJupiter()
        }
        functionalTest(JvmTestSuite) {
            useSpock()

            targets {
                all {
                    testTask.configure {
                        shouldRunAfter(tasks.test)
                    }
                }
            }
        }
    }
}

tasks.named('check') {
    dependsOn(testing.suites.functionalTest)
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}


gradlePlugin {
    plugins {
        archUnitGradlePlugin {
            id = 'com.societegenerale.commons.arch-unit-gradle-plugin'
            implementationClass = 'com.societegenerale.commons.plugin.gradle.ArchUnitGradlePlugin'
        }
    }

    testSourceSets(sourceSets.functionalTest)
}

pluginBundle {
    website = 'https://github.com/societe-generale/arch-unit-gradle-plugin'
    vcsUrl = 'https://github.com/societe-generale/arch-unit-gradle-plugin'
    description = 'A Gradle plugin to run packaged ArchUnit rules'
    tags = ['archUnit']

    plugins {
        archUnitGradlePlugin {
            // id is captured from java-gradle-plugin configuration
            displayName = 'A Gradle plugin to run packaged ArchUnit rules'
        }
    }
}
